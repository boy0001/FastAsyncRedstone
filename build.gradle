apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/"}
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:1.7.0'
    }
}

repositories {
    maven {url "https://hub.spigotmc.org/nexus/content/groups/public/"}
    maven {url "http://empcraft.com/maven2"}
    maven {url "http://repo.mcstats.org/content/repositories/public"}
    maven {url "http://maven.sk89q.com/repo/"}
    maven {url "http://repo.maven.apache.org/maven2"}
    maven {url "http://ci.athion.net/job/FastAsyncWorldEdit/ws/mvn/"}
    maven {url "http://ci.regularbox.com/plugin/repository/everything/"}
    maven {url "http://maven.sk89q.com/artifactory/repo/"}
}

dependencies {
    compile 'org.ajoberstar:grgit:1.7.0'
    compile group: "com.boydti", name: "fawe-api", version: "latest", changing: true
    compile 'org.bukkit.craftbukkitv1_10:craftbukkitv1_10:1.10'
    compile 'org.bukkit.craftbukkit:CraftBukkit:1.8.8'
    compile(group: 'com.sk89q.worldedit', name: 'worldedit-core', version:'6.1.3-SNAPSHOT') {
        exclude(module: 'bukkit-classloader-check')
    }
}

def revision = ""
def buildNumber = ""
def semver = ""
def date = ""
ext {
    git = org.ajoberstar.grgit.Grgit.open(file(".git"))
    date = git.head().date.format("yy.MM.dd")
    revision = "-${git.head().abbreviatedId}"
    parents = git.head().parentIds;
    index = 0;  // Offset to mach CI
    int major, minor, patch;
    major = minor = patch = 0;
    for (;parents != null && !parents.isEmpty();index++) {
        int majorCount, minorCount, patchCount;
        patchCount = minor == 0 && major == 0 ? 1 : 0;
        commit = git.getResolve().toCommit(parents.get(0));
        for (String line : commit.fullMessage.tokenize("\n")) {
            switch (line.replaceAll("- ", "").split(" ")[0].toLowerCase()) {
                case "minor":
                case "added":
                case "add":
                case "change":
                case "changed":
                case "changes":
                    if (major == 0) {minorCount = 1; patchCount = 0;}
                    break;
                case "refactor":
                case "remove":
                case "major":
                    patchCount = minorCount = 0;
                    majorCount = 1;
                    break;
            }
        }
        major += majorCount;
        minor += minorCount;
        patch += patchCount;
        parents = commit.getParentIds()
    }
    buildNumber = "-${index}"
    semver = "-${major}.${minor}.${patch}"
}

version = date + revision + buildNumber + semver
if ( project.hasProperty("lzNoVersion") ) { // gradle build -PlzNoVersion
    version = "unknown";
}
description = rootProject.name

clean{
    delete file("target")
}

processResources {
    from('src/main/resources') {
        include 'plugin.yml'
        include 'fawe.properties'
        expand(
            name: project.name,
            version: project.version
        )
    }
}
jar {
    enabled = true
    archiveName = "${rootProject.name}-${project.version}.jar"
    destinationDir = file 'target'
}
println sourceSets.main.output.classesDir